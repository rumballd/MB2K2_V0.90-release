        NAM    STAR TRECK
*       VER 1.0
*
*
        ORG    $0020
STPSFL  RMB    1
BASEX   RMB    1
BASEY   RMB    1
BASESX  RMB    1
BASESY  RMB    1
GLMFLG  RMB    1
SECKLN  RMB    1
FLAGC   RMB    1
TIMDEC  RMB    1
STCFLG  RMB    1
SQFLG   RMB    1
PNTFLG  RMB    1
COURSE  RMB    1
WARP    RMB    1
FINCX   RMB    1
FINCY   RMB    1
COUNT   RMB    1
TEMP    RMB    2
TEMP2   RMB    2
XTEMP   RMB    2
TIME0   RMB    2
GAMTIM  RMB    1
TIMUSE  RMB    2
SHENGY  RMB    2
KLNENG  RMB    2
PHSENG  RMB    2
TOPFLG  RMB    1
BOTFLG  RMB    1
LSDFLG  RMB    1
RSDFLG  RMB    1
PHOTON  RMB    1
CURQUX  RMB    1
CURQUY  RMB    1
CURSCX  RMB    1
CURSCY  RMB    1
TRIALX  RMB    1
TRIALY  RMB    1
FLAG    RMB    1
CNDFLG  RMB    1
SCANX   RMB    1
SCANY   RMB    1
COUNT1  RMB    1
SECINF  RMB    1
MASK    RMB    1
KLNGCT  RMB    1
LENGTH  RMB    1
ASAVE   RMB    1
SHIELD  RMB    1
ENERGY  RMB    2
TSAVE1  RMB    1
HITKLS  RMB    1
HITSTR  RMB    1
HITBAS  RMB    1
TEMP3   RMB    2
GALCNT  RMB    1
DAMENG  RMB    1
DAMSRS  RMB    1
DAMLRS  RMB    1
DAMPHS  RMB    1
DAMPHT  RMB    1
DAMSHL  RMB    1
SUPFLG  RMB    1
TELFLG  RMB    1
ATKENG  RMB    2
QUDPTR  RMB    2
PASWRD  RMB    3
PHTFLG  RMB    1
QUDMAP  RMB    64
SECMAP  RMB    16

*       TABLE OF MOVE VECTORS
MOVTBL  FCB    0,$FF,1,$FF,1,0,1,1,0,1,$FF,1,$FF,0,$FF,$FF

*       SPECIAL CHARACTER TABLE
CHRTBL  FCC    '.*KB'

*       COMMAND JUMP TABLE.
JMPTBL  FDB    SETCRS
        FDB    SRSCAN
        FDB    LRSCAN
        FDB    PHASER
        FDB    PHOTOR
        FDB    DAMRPT
        FDB    SHLDUP
        FDB    SHLDWN
        FDB    TELEPT
        FDB    SELFDE
OUTCH   JMP    $CD18
INCH    JMP    $CD0C
RANDOM  JMP    RAND

        ORG    $0100
PD1     JSR    OUTCH
PDATA1  LDA    ,X+
        CMPA   #4
        BNE    PD1
        RTS
SPAVOY CLRA
 TFR A,DP
 LDA #$0C
 LDS #$8000
 JSR OUTCH
 LDX #TITLE
 JSR PSTRNG
 LDX #STPSFL
SETUP CLRA
 STA ,X+
 CPX #MOVTBLE
 BNE SETUP
 LDX #SHTLNG
 JSR PSTRNG
 JSR INCH
 CMPA #'S
 BEQ SHORT
 INC LENGTH
SHORT LDX #QUDMAP
 LDB #64
SETUP0 JSR RANDOM
 CMPA #$FC
 BLS SETUP1
 LDA #4
 BRA SETUP5
SETUP1 CMPA #$F7
 BLS SETUP2
 LDA #$3
 BRA SETUP5
SETUP2 CMPA #$E0
 BLS SETUP3
 LDA #$2
 BRA SETUP5
SETUP3 CMPA #$A0
 BLS SETUP4
 LDA #$1
 BRA SETUP5
SETUP4 CLRA
SETUP5 CLR ASAVE
 TST LENGTH
 BEQ SETUP8
 STA ASAVE
 JSR RANDOM
 CMPA #$F0
 BLS SETUP6
 LDA #$3
 BRA SETUP8
SETUP6 CMPA #$C0
 BLS SETUP7
 LDA #$2
 BRA SETUP8
SETUP7 CLRA
SETUP8 ADDA ASAVE
 STA ,X
 ADDA KLNGCT
 STA KLNGCT
STARS JSR RANDOM
 ANDA #$38
 ORA ,X
 STA ,X
CONT INX
 DECB
 BEQ CONT1
 BRA SETUP0
CONT1 JSR RANDOM
 ANDA #7
 TAB
 JSR RANDOM
 ANDA #7
 STA BASEX
 STB BASEY
 INC STPSFL
 LDX #QUDMAP
 JSR STPSEX
 LDA #$40
 ORA ,X
 STA ,X
CONT2 BSR REFUEL
 JSR RANDOM
 ADDA #0
 DAA
 STA TIME0+1
 JSR RANDOM
 ANDA #$7F
 ORA #$10
 ADDA #0
 DAA
 STA TIME0
 BSR MAKTIM
 TST LENGTH
 BEQ GATM
 TAB
 BSR MAKTIM
 SUBA #3
 ABA
 DAA
GATM STA GAMTIM
 BRA CONT3
*
* REFUEL THE ENTERPRISE.
REFUEL CLR SHIELD
 LDA #$30
 STA ENERGY
 STA SHENGY
 CLR ENERGY+1
 CLR SHENGY+1
 LDA #15
 STA PHOTON
 LDX #DAMENG
REFUL1 CLR ,X+
 CPX #SUPFLG
 BNE REFUL1
 RTS
*
MAKTIM JSR RANDOM
 ANDA #$0F
 ORA #$22
 ADDA #0
 DAA
 RTS
* SETUP CONTINUED.
CONT3 JSR RANDOM
 ANDA #7
 STA CURQUX
 JSR RANDOM
 ANDA #7
 STA CURQUY
 JSR SETUPS
 LDX #BASINF
 JSR PSTRNG
 LDA BASEX
 BSR FIXOUT
 JSR OUTDSH
 LDA BASEY
 BSR FIXOUT
 LDX #INTRO0
 JSR PSTRNG
 LDX #PASWRD
 LDB #3
CONT4 JSR INCH
 STA ,X+
 DECB
 BNE CONT4
 LDX #INTRO1
 JSR PSTRNG
 BSR OUTDAT
 LDX #INTRO2
 JSR PSTRNG
 BSR OUTKLN
 LDX #INTRO3
 JSR PSTRNG
 CLR TEMP
 LDA GAMTIM
 STA TEMP+1
 JSR OUTBCD
 LDX #INTRO4
 JSR PSTRNG
 BSR OUTQUD
 LDX #INTRO6
 JSR PSTRNG
 BSR OUTSEC
 JMP COMAND
* OUTPUT A NUMBER.
FIXOUT ADDA #$31
 JMP OUTCH
* OUTPUT STARDATE.
OUTDAT LDX TIME0
 STX TEMP
 JSR OUTBCD
 LDA #'.
 JSR OUTCH
 LDA TIMDEC
 JMP OUTHR
* OUTPUT KLINGON COUNT.
OUTKLN LDA KLNGCT 
OUTK0 CLR TEMP
OUTK1 CLR TEMP+1
 LDB #10
OUTK2 SBA
 BCS OUTK3
 INC TEMP+1
 CMPB TEMP+1
 BNE OUTK2
 INC TEMP
 BRA OUTK1
OUTK3 ABA
 LDB TEMP+1
 ASLB
 ASLB
 ASLB
 ASLB
 ABA
 STA TEMP+1
 JMP OUTBCD
* OUTPUT QUDRANT.
OUTQUD LDA CURQUX
 BSR FIXOUT
 BSR OUTDSH
 LDA CURQUY
 BRA FIXOUT
* OUTPUT SECTOR
OUTSEC LDA CURSCX
 BSR FIXOUT
 BSR OUTDSH
 LDA CURSCY
 BRA FIXOUT
* OUTPUT A DASH.
OUTDSH LDA #'-
 JMP OUTCH
* FIX THE INDEX REGISTER.
FIXXRG STX TEMP
 ADDA TEMP+1
 STA TEMP+1
 LDX TEMP
 RTS
*
* MAIN PROGRAM LOOP.
COMAND LDA GAMTIM
 CMPA TIMUSE+1
 BHI NOEXTC
 JMP NOMTIM
NOEXTC TST KLNGCT
 BNE NOEXT2
 JMP NOMKLN
NOEXT2 TST SUPFLG
 BEQ NOEXT4
 LDX #SUPDES
 JSR PSTRNG
 JMP SELFDA
NOEXT4 JSR CLRCQU
 LDA #2
 CMPA CNDFLG
 BEQ CMND27
 CLR CNDFLG
 TST SECKLN
 BEQ CMNDAC
 DECA
 STA CNDFLG
CMNDAC JSR RANDOM
 CMPA #$FC
 BLS COMND2
 LDX #SPSTRM
 JSR PSTRNG
 LDA #2
 STA DAMSHL
 JMP SHLDWN
COMND2 JSR RANDOM
 CMPA #$FC
 BLS CMND25
 JMP SUPNOV
CMND25 JSR ATTACK
 TST ENERGY
 BPL COMND0
 JMP NRGOUT
COMND0 LDA #3
 CMPA ENERGY
 BLS CMNDAD
 STA CNDFLG
CMNDAD TST SHENGY
 BPL CMND27
 CLRA
 STA SHIELD
 STA SHENGY
 STA SHENGY+1
CMND27 LDX #COMST
 JSR PSTRNG
 CLR STCFLG
 CLR PHTFLG
COMND3 JSR INCHCK
COMND4 ASLA
 LDX #JMPTBL
 JSR FIXXRG
 LDX ,X
 JMP ,X
* OUTPUT A FOUR DIGIT BCD NUMBER.
OUTBCD CLR FLAG
 LDA TEMP
 BEQ OUTBC2
 ANDA #$F0
 BEQ OUTBC1
 LDA TEMP
 JSR OUTHL
OUTBC1 LDA TEMP
 JSR OUTHR
 INC FLAG
OUTBC2 LDA TEMP+1
 TST FLAG
 BNE NOZERO
 ANDA #$F0
 BEQ OUTBC3
NOZERO JSR OUTHL
OUTBC3 LDA TEMP+1
 JMP OUTHR
* LOWER SHIELDS
SHLDWN CLR SHIELD
 LDX #DWNST
SHLD0 JSR PSTRNG
SHLD1 JMP COMAND
* RAISE SHIELDS
SHLDUP LDA SHENGY
 CMPA #1
 BLS SHLD1
 TST DAMSHL
 BNE SHLDWN
 INC SHIELD
 LDX #SHENGY
 LDA #2
 STA TEMP
 CLR TEMP+1
 JSR BCDSUB
 LDX #UPSTR
 BRA SHLD0
* SHORT RANGE SCAN.
SRSCAN TST DAMSRS
 BEQ SSCAN1
 JMP RPTDAM
SSCAN1 JSR PCRLF
 CLR SCANY
 LDA CNDFLG
 CMPA #2
 BNE SSCAN2
 JSR REFUEL
SSCAN2 LDX #SECMAP
 STX TEMP2
 JSR DOSCAN
 LDX #SDATE
 JSR PDATA1
 JSR OUTDAT
 JSR DOSCAN
 LDX #CNDTNS
 JSR PDATA1
 LDA CNDFLG
 BEQ SRSCN1
 CMPA #1
 BEQ SRSCN0
 CMPA #3
 BEQ OUTCN1
 LDX #DOCKED
 BRA OUTCND
OUTCN1 LDX #YELLOW
OUTCND JSR PDATA1
 BRA SRSCN2
SRSCN0 LDX #RED
 BRA OUTCND
SRSCN1 LDX #GREEN
 BRA OUTCND
SRSCN2 BSR DOSCAN
 LDX #QUADP
 JSR PDATA1
 JSR OUTQUD
 BSR DOSCAN
 LDX #SECP
 JSR PDATA1
 JSR OUTSEC
 BSR DOSCAN
 LDX #ENGSTR
 JSR PDATA1
 LDX ENERGY
 STX TEMP
 JSR OUTBCD
 BSR DOSCAN
 LDX #KLSTR
 JSR PDATA1
 JSR OUTKLN
 BSR DOSCAN
 LDX #SHSTR
 JSR PDATA1
 LDX SHENGY
 STX TEMP
 JSR OUTBCD
 TST SHIELD
 BEQ SRSCN4
 LDX #UPSCAS
 BRA SRSCN5
SRSCN4 LDX #DNSCAS
SRSCN5 JSR PDATA1
 BSR DOSCAN
 LDX #TRPSTR
 JSR PDATA1
 CLR TEMP
 LDA PHOTON
 ADDA #0
 DAA
 STA TEMP+1
 JSR OUTBCD
 JMP COMAND
* OUTPUT ONE SHORT RANGE SCAN LINE
DOSCAN JSR PCRLF
 LDA #2
 STA COUNT
 CLR SCANX
DOSCN LDA #4
 STA COUNT1
 LDX TEMP2
 LDA ,X
DOSCN0  STA ASAVE
 LDA CURSCY
 CMPA SCANY
 BNE CHK0
 LDA CURSCX
 CMPA SCANX
 BNE CHK0
 LDA #'E
 JSR OUTCH
 BRA GOAHD
CHK0 LDA ASAVE
DOSCN1 LDX #CHRTBL
 ANDA #$3
 JSR FIXXRG
 LDA ,X
 JSR OUTCH
GOAHD JSR OUTS
 LDX TEMP2
 INC SCANX
 DEC COUNT1
 BEQ DOSCN2
 LDA ASAVE
 LSRA
 LSRA
 BRA DOSCN0
DOSCN2 LEAX 1,X
 STX TEMP2
 LDA ,X
 DEC COUNT
 BNE DOSCN
 INC SCANY
 RTS
* PRINT STRING ROUTINE
PSTRNG BSR PCRLF
 JMP PDATA1
* PRINT CRLF.
PCRLF STX XTEMP
 LDX #CRLFST
 JSR PDATA1
 LDX XTEMP
 RTS
* SETUP SECTOR MAP
SETUPS CLR SECKLN
 LDX #SECMAP
 LDB #16
 CLRA
SETPS1 STA ,X+
 DECB
 BNE SETPS1
 LDX #QUDMAP
 LDA CURQUX
 LDB CURQUY
STPSEX STA ASAVE
 TSTB
 BEQ SETPS4
SETPS2 LDA #8
SETPS3 INX
 DECA
 BNE SETPS3
 DECB
 BNE SETPS2
SETPS4 LDA ASAVE
 BEQ SETPS6
SETPS5 INX
 DECA
 BNE SETPS5
SETPS6 TST STPSFL
 BEQ STPSNX
 CLR STPSFL
 RTS
STPSNX STX QUDPTR
 LDB ,X
 STB SECINF
 BEQ SETP10
 ANDB #7
 BEQ SETPS7
 STB COUNT
 STB SECKLN
 LDA #2
 STA MASK
 BSR PUTINM
SETPS7 LDB SECINF
 ANDB #$38
 BEQ SETPS8
 LSRB
 LSRB
 LSRB
 STB COUNT
 LDA #1
 STA MASK
 BSR PUTINM
SETPS8 LDB SECINF
 BITB #$40
 BEQ SETPS9
 LDA #1
 STA COUNT
 LDA #3
 STA MASK
 BSR PUTINM
SETPS9 LDB SECINF
 BPL SETP10
 INC SUPFLG
SETP10 JSR RANDOM
 ANDA #7
 STA TRIALX
 JSR RANDOM
 ANDA #7
 STA TRIALY
 JSR CHKPOS
 TST FLAG
 BNE SETP10
 LDA TRIALX
 STA CURSCX
 LDA TRIALY
 STA CURSCY
* CALCULATE LOCAL KLINGON ENERGY
CSCKEN LDB SECKLN
 ASLB
 LDX #KLNENG
CSCEXT CLR ,X
 CLR 1,X
CSCKN1 JSR RANDOM
 ADDA #0
 DAA
 CLR TEMP
 STA TEMP+1
 JSR BCDADD
 DECB
 BNE CSCKN1
 RTS
* PUT OBJECTS IN SECTOR MAP
PUTINM LDX #SECMAP
 JSR RANDOM
 ANDA #$0F
 STA TSAVE1
 JSR FIXXRG
 LDB ,X
 JSR RANDOM
 ANDA #3
 STA ASAVE
 BEQ PUTIN2
PUTIN1 RORB
 RORB
 DECA
 BNE PUTIN1
PUTIN2 BITB #3
 BNE PUTINM
 ORB MASK
 LDA ASAVE
 BEQ PUTIN4
PUTIN3 ROLB
 ROLB
 DECA
 BNE PUTIN3
PUTIN4 STB ,X
 DEC COUNT
 BNE PUTINM
 LDA MASK
 CMPA #3
 BNE PUTIN6
 LDB TSAVE1
 CLC
 RORB
 LDA ASAVE
 BCC PUTIN5
 ADDA #4
PUTIN5 STA BASESX
 STB BASESY
PUTIN6 RTS
* CHECK FOR EMPTY SECTORS.
CHKPOS CLR FLAG
 STX XTEMP
 LDX #SECMAP
 LDA TRIALY
 BEQ CHKPO2
CHKPO1 INX
 INX
 DECA
 BNE CHKPO1
CHKPO2 LDA TRIALX
 CMPA #3
 BLS CHKPO3
 INX
 SUBA #4
CHKPO3 LDB ,X
 STA ASAVE
 BEQ CHKPO5
CHKPO4 LSRB
 LSRB
 DECA
 BNE CHKPO4
CHKPO5 ANDB #3
 BEQ CHKPO6
 STB FLAG
 TST STCFLG
 BEQ CHKPO6
 TST PHTFLG
 BNE CHKPO7
 CMPB #2 
 BEQ CHKPO7
CHKPO6 LDX XTEMP
 RTS
CHKPO7 LDB #$FC
 LDA ASAVE
 BEQ CHKPO9
 SEC
CHKPO8 ROLB
 ROLB
 DECA
 BNE CHKPO8
CHKPO9 ANDB ,X
 STB ,X
 BRA CHKPO6
* FIRE PHOTON TORPEDOES.
PHOTOR TST DAMPHT
 BEQ PTRNDM
PTRND9 JMP RPTDAM
PTRNDM TST PHOTON
 BNE PHOTR0
 JMP PTEMPT
PHOTR0 INC PHTFLG
 JSR RANDOM
 ANDA #$F
 ORA #4
 STA WARP
 DEC PHOTON
* WARP ENGINES.
SETCRS CLR SQFLG
 CLR GLMFLG
 LDX #CRSSTR
 JSR PSTRNG
 JSR INCHCK
 CMPA #7
 BHI ABC29
 STA COURSE
 TST PHTFLG
 BNE PHTOR1
 LDX #WRPSTR
 JSR PSTRNG
 JSR INCHCK
 STA WARP
 TST PNTFLG
 BNE STCRS2
 STA SQFLG
 TST DAMENG
 BNE PTRND9
 BRA STCRS2
STCRS2 JSR INCH
 CMPA #$0D
 BNE STCRS2
 LDA COURSE
PHTOR1 LDX #MOVTBL
 ASLA
 JSR FIXXRG
 LDA WARP
 BNE ABC30
ABC29 JMP COMAND
ABC30 STA COUNT
 TST SQFLG
 BEQ STCRS3
 LDA #$0F
 STA COUNT
STCRS3 LDA CURSCX
 LDB CURSCY
PHTOR2 ADDA ,X
 ADDB 1,X
 JSR TSTBND
 TST FINCX
 BEQ ABC1
 JMP STCRS5
ABC1 TST FINCY
 BEQ ABC5
 JMP STCRS5
ABC5 STA TRIALX
 STB TRIALY
 TST PHTFLG
 BEQ ABC4
 JSR PCRLF
 BSR OBATSO
ABC4 INC STCFLG
 JSR CHKPOS
 LDA FLAG
 BEQ STCRS4
 TST PHTFLG
 BEQ ABC2
 JMP PHTOR3
ABC2 CMPA #2
 BNE ABC3
 JMP KLGRAM
ABC3 LDX #BLOKST
 JSR PSTRNG
OBATSO LDA TRIALX
 JSR FIXOUT
 JSR OUTDSH
 LDA TRIALY
 JSR FIXOUT
 TST PHTFLG
 BEQ STCRET
 RTS
STCRET JMP STCRS6
STCRS4 TST PHTFLG
 BEQ STCRSB
 LDA TRIALX
 LDB TRIALY
 DEC COUNT
 BNE PHTOR2
 JMP PHTOR4
STCRSB LDA TRIALX
 STA CURSCX
 LDA TRIALY
 STA CURSCY
 JSR RANDOM
 CMPA #$80
 BLS STCRSC
 LDA #1
 JSR FIXTIM
 LDA #3
 JSR FIXENG
STCRSC DEC COUNT
 BEQ STCRSD
 JMP STCRS3
STCRSD TST SQFLG
 BNE STCRS5
 JMP STCRS6
STCRS5 TST PHTFLG
 BEQ ABC6
 JMP PHTOR4
ABC6 LDA WARP
 STA COUNT
ABC7 LDA CURQUX
 LDB CURQUY
 TST SQFLG
 BEQ STCRS7
 ADDA ,X
 ADDB 1,X
 STA TRIALX
 STB TRIALY
 JSR TSTBND
 TST FINCX
 BEQ ABCD0
 JMP GALBND
ABCD0 TST FINCY
 BEQ ABCD1
 JMP GALBND
ABCD1 LDA TRIALX
 STA CURQUX
 LDA TRIALY
 STA CURQUY
 INC GLMFLG
 LDA #6
 JSR FIXTIM
 LDA #$30
 JSR FIXENG
 DEC COUNT
 BNE ABC7
STCRSA JSR SETUPS
STCRS6 CLR CNDFLG
 LDA BASEX
 CMPA CURQUX
 BNE SEXIT1
 LDB BASEY
 CMPB CURQUY
 BNE SEXIT1
 LDA BASESX
 LDB BASESY
 INCA
 SUBA CURSCX
 CMPA #2
 BHI SEXIT1
SEXIT0 INCB
 SUBB CURSCY
 CMPB #2
 BHI SEXIT1
SDOCK LDA #2
 STA CNDFLG
SEXIT1 JMP COMAND
STCRS7 ADDA FINCX
 ADDB FINCY
 CMPA #7
 BHI GALBND
 CMPB #7
 BHI GALBND
 STA CURQUX
 STB CURQUY
 LDA #7
 JSR FIXTIM
 BRA STCRSA
* RAMMED A KLINGON.
KLGRAM LDX #KRMSTR
 JSR PSTRNG
 LDA #1
 STA COUNT
 CLR SQFLG
 INC HITKLS
 DEC KLNGCT
 DEC SECKLN
 INC PHTFLG
 JSR OBATSO
 LDX #HEVDAM
 JSR PSTRNG
 LDB #$6A
 JSR MANDAM
 LDX #STILFT
 JSR PSTRNG
 JSR OUTKLN
 JMP STCRSB
* REPORT GALAXY LIMIT.
GALBND LDX #GLBNDS
 JSR PSTRNG
 LDA GALCNT
 INCA
 CMPA #3
 BNE GALBN2
 LDX #GALDUM
 JSR PSTRNG
 JMP ENDGAM
GALBN2 STA GALCNT
 TST GLMFLG
 BNE ABC20
 JMP STCRS6
ABC20 JMP STCRSA
* TORPEDO HIT OBJECT.
PHTOR3 LDX #PHITST
 JSR PSTRNG
 LDA FLAG
 CMPA #2
 BNE ABC8
 DEC SECKLN
 DEC KLNGCT
 INC HITKLS
 LDX #KLGSTR
 JSR PDATA1
 LDX #STILFT
 JSR PSTRNG
 JSR OUTKLN
 BRA ABC10
ABC8 CMPA #1
 BNE ABC11
 INC HITSTR
 LDX #STARST
ABC9 JSR PDATA1
ABC10 JMP COMAND
ABC11 LDX #BASEST
 INC HITBAS
 BRA ABC9
* TORPEDO OUT OF ENERGY
PHTOR4 LDX #PNOENG
 JSR PSTRNG
 JMP STCRS6
* TEST FOR OUT OF BOUNDS.
TSTBND CLR FINCX
 CLR FINCY
 TSTA
 BPL TSTBN1
 DEC FINCX
 BRA TSTBN2
TSTBN1 CMPA #7
 BLS TSTBN2
 INC FINCX
TSTBN2 TSTB
 BPL TSTBN3
 DEC FINCY
 RTS
TSTBN3 CMPB #7
 BLS TSTBN4
 INC FINCY
TSTBN4 RTS
* TORPEDO TUBES EMPTY.
PTEMPT LDX #PTEMST
 JSR PSTRNG
 JMP COMAND
* INPUT CHARACTER AND CHECK
INCHCK CLRA
INCHK0 STA PNTFLG
 JSR INCH
 CMPA #'.
 BEQ INCHK0
 CMPA #$39
 BHI INCHK1
 CMPA #$2F
 BLS INCHK1
 SUBA #$30
 RTS
INCHK1 LDX #ERR
 JSR PDATA1
 BRA INCHCK
* FIX UP TIME
FIXTIM ADDA TIMDEC
 CMPA #9
 BLS FIXTM1
 SUBA #10
 STA TIMDEC
 CLR TEMP
 LDA #1
 STA TEMP+1
 STX XTEMP
 LDX #TIME0
 JSR BCDADD
 LDX #TIMUSE
 JSR BCDADD
 JSR FIXDAM
 LDX XTEMP
 RTS
FIXTM1 STA TIMDEC
 RTS
* FIX UP ENERGY.
FIXENG STX XTEMP
 LDX #ENERGY
 CLR TEMP
 STA TEMP+1
 JSR BCDSUB
 LDX XTEMP
 RTS
* BCD ADDITION
BCDADD CLC
 BRA BCDFIX
* BCD SUBTRACT
BCDSUB LDA #$99
 SUBA TEMP
 STA TEMP
 LDA #$99
 SUBA TEMP+1
 STA TEMP+1
 SEC
*BCD FIX
BCDFIX LDA 1,X
 ADCA TEMP+1
 DAA
 STA 1,X
 LDA ,X
 ADCA TEMP
 DAA
 STA ,X
 RTS
* LONG RANGE SCAN.
LRSCAN TST DAMLRS
 BEQ LRSNDM
 JMP RPTDAM
LRSNDM CLR TOPFLG
 CLR BOTFLG
 CLR LSDFLG
 CLR RSDFLG
 LDX #LRSCST
 JSR PSTRNG
 JSR OUTQUD
 JSR PCRLF
 JSR PCRLF
 LDA CURQUX
 BNE LRSCN1
 INC LSDFLG
LRSCN1 CMPA #7
 BNE LRSCN2
 INC RSDFLG
LRSCN2 LDA CURQUY
 BNE LRSCN3
 INC TOPFLG
LRSCN3 CMPA #7
 BNE LRSCN4
 INC BOTFLG
LRSCN4 LDX QUDPTR
LRSCNC LDA #$F7
 JSR FIXXRG
 TST TOPFLG
 BEQ LRSCN7
 BSR OUTTHO
 BRA LRSCN8
LRSCN7 BSR OUTLIN
LRSCN8 LDA #5
 JSR FIXXRG
 BSR OUTLIN
 LDA #5
 JSR FIXXRG
 TST BOTFLG
 BEQ LRSCN9
 BSR OUTTHO
 BRA LRSC10
LRSCN9 BSR OUTLIN
LRSC10 JMP COMAND
OUTLIN LDA ,X
 TST LSDFLG
 BEQ OUTLN1
 CLRA
OUTLN1 BSR OUTQIN
 LDA ,X
 BSR OUTQIN
 LDA ,X
 TST RSDFLG
 BEQ OUTLN2
 CLRA
OUTLN2 BSR OUTQIN
 JMP PCRLF
* OUTPUT QUADRANT INFO.
OUTQIN TAB
 ANDA #$80
 CLC
 ROLA
 ROLA
 JSR OUTHR
 TBA
 ANDA #$40
 LSRA
 LSRA
 JSR OUTHL
 TBA
 ANDA #$38
 ASLA
 JSR OUTHL
 TBA
 ANDA #$07
 JSR OUTHR
 INX
 JMP OUTS
* OUTPUT A LINE OF ZEROES.
OUTTHO LDA #3
 STA COUNT
OUTTH1 CLRA
 BSR OUTQIN
 DEC COUNT
 BNE OUTTH1
 JMP PCRLF
* FIRE PHASERS.
PHASER TST DAMPHS
 BEQ PHSNDM
 JMP RPTDAM
PHSNDM TST SHIELD
 BEQ PHASR1
 LDX #MLSHLD
 BRA TOOMC1
PHASR1 LDX #ENAVLB
 JSR PSTRNG
 LDX ENERGY
 STX TEMP
 JSR OUTBCD
 LDX #FIRENG
 JSR PSTRNG
 JSR INBCD
 LDA PHSENG
 CMPA ENERGY
 BHI TOOMCH
 BNE PHASR2
 LDA PHSENG+1
 CMPA ENERGY+1
 BLS PHASR2
TOOMCH LDX #TOMUCH
TOOMC1 JSR PSTRNG
 JMP COMAND
PHASR2 JSR RANDOM
 CMPA #$F4
 BLS PHASR3 
 LDX #PHAMIS
 JSR PSTRNG
 BRA PHASR6
PHASR3 LDA PHSENG
 CMPA KLNENG
 BHI PHASR4
 BNE PHASR5
 LDA PHSENG+1
 CMPA KLNENG+1
 BLS PHASR5
PHASR4 CLR KLNENG
 CLR KLNENG+1
 LDX #ALKILL
 JSR PSTRNG
 LDX #STILFT
 JSR PSTRNG
 LDB SECKLN
 STB HITKLS
 LDA KLNGCT
 SBA
 STA KLNGCT
 JSR OUTKLN
 CLR SECKLN
KILALK LDX #SECMAP
 LDA #16
 STA COUNT
KILAL1 LDB #4
 LDA ,X
KILAL2 RORA
 BCS KILAL4
 RORA
 BCC KILAL3
 CLC
KILAL3 DECB
 BNE KILAL2
 RORA
 STA ,X
 INX
 DEC COUNT
 BNE KILAL1
 BRA PHASR6
KILAL4 RORA
 BRA KILAL3
PHASR5 LDX PHSENG
 STX TEMP
 LDX #KLNENG
 JSR BCDSUB
 LDX #KHTADM
 JSR PSTRNG
PHASR6 LDX PHSENG
 STX TEMP
 LDX #ENERGY
 JSR BCDSUB
 JMP COMAND
* INPUT A BCD NUMBER.
INBCD CLR PHSENG
 CLR PHSENG+1
INBCD1 JSR INCH
 CMPA #$2F
 BLS EXIT
 BSR CHECK
 TST FLAGC
 BNE INEROR
 LDB #4
INBCD2 ASL PHSENG+1
 ROL PHSENG
 DECB
 BNE INBCD2
 ADDA PHSENG+1
 STA PHSENG+1
 BRA INBCD1
INEROR LDX #ERR
 JSR PDATA1
 BRA INBCD1
EXIT RTS
CHECK CLR FLAGC
 CMPA #$39
 BHI SETFLG
 ANDA #$0F
 RTS
SETFLG INC FLAGC
 RTS
* SELF DESTRUCT.
SELFDE LDX #INTRO0
 JSR PSTRNG
 LDX #PASWRD
 LDB #3
SELFD1 JSR INCH
 CMPA ,X
 BNE SELFD2
 INX
 DECB
 BNE SELFD1
SELFDA LDX #DISINT
 JSR PSTRNG
 JMP ENDGAM
SELFD2 LDX #ABORT
 JSR PSTRNG
 JMP COMAND
* TELEPORTER.
TELEPT LDA #$12
 CMPA TIMUSE+1
 BHI TELEP2
 TST TELFLG
 BNE TELEP4
 JSR RANDOM
 CMPA #$B0
 BLS TELEP1
 INC TELFLG
TELEP1 JSR RANDOM
 CMPA #$B0
 BHI TELEP5
 LDA BASEX
 STA CURQUX
 LDA BASEY
 STA CURQUY
TELEPA JMP STCRSA
TELEP2 LDX #CANTUS
TELEP3 JSR PSTRNG
 JMP COMAND
TELEP4 LDX #DMGDST
 BRA TELEP3
TELEP5 JSR RANDOM
 ANDA #7
 STA CURQUX
 JSR RANDOM
 ANDA #7
 STA CURQUY
 LDX #SOMWHR
 JSR PSTRNG
 BRA TELEPA
* KLINGONS ATTACK
ATTACK TST SECKLN
 BNE ATTAC1
 RTS
ATTAC1 JSR RANDOM
 CMPA #$B0
 BHI ATTAC2
 LDX #ATKENG
 LDB SECKLN
 ASLB
 JSR CSCEXT
 LDX ATKENG
 STX TEMP
 TST SHIELD
 BNE ATTAC3
 LDX #ENERGY
 JSR BCDSUB
 JSR PCRLF
 JSR PCRLF
 LDX ATKENG
 STX TEMP
 JSR OUTBCD
 LDX #KATKDN
 JSR PDATA1
 LDB #$FA
 JSR MANDAM
ATTAC2 RTS
ATTAC3 LDX #SHENGY
 JSR BCDSUB
 LDX #KATKUP
 JMP PSTRNG
* END OF GAME CLEANUP.
NRGOUT LDX #NMENGS
NRGOU1 JSR PSTRNG
 BRA ENDGAM
NOMTIM LDX #NMTMST
 BRA NRGOU1
NOMKLN LDX #NMKLST
 JSR PSTRNG
 BRA ENDGM2
ENDGAM LDX #FAILST
 JSR PSTRNG
 BRA ENDGM3
ENDGM2 LDX #SUCCST
 JSR PSTRNG
ENDGM3 LDX #PLAYAG
 JSR PSTRNG
 JSR INCH
 CMPA #'N
 BEQ ENDGM4
 JMP SPAVOY
ENDGM4 JMP $CD03
* CLEAR OUT CURRENT QUADRENT
CLRCQU LDX QUDPTR
 LDA ,X
 SUBA HITKLS
 LDB HITSTR
 ASLB
 ASLB
 ASLB
 SBA
 TST HITBAS
 BEQ CLRCQ2
 ANDA #$BF
 LDB #$0A
 STB BASEX
 STB BASEY
 CLR CNDFLG
CLRCQ2 STA ,X
 CLRA
 STA HITKLS
 STA HITSTR
 STA HITBAS
 RTS
* FIX DAMAGES.
FIXDAM LDX #DAMENG
 LDB #6
FIXDM1 TST ,X
 BEQ FIXDM2
 DEC ,X
FIXDM2 INX
 DECB
 BNE FIXDM1
 RTS
* SUPERNOVA GENERATOR.
SUPNOV JSR RANDOM
 ANDA #7
 TAB
 JSR RANDOM
 ANDA #7
 STB TSAVE1
 LDX #QUDMAP
 INC STPSFL
 JSR STPSEX
 LDB ,X
 ANDB #7
 LDA KLNGCT
 SBA
 STA KLNGCT
 LDA #$80
 STA ,X
 LDX #SUPSTR
 JSR PSTRNG
 LDA ASAVE
 JSR FIXOUT
 JSR OUTDSH
 LDA TSAVE1
 JSR FIXOUT
 JMP COMAND
MANDAM LDX #DAMENG
MANDM1 JSR RANDOM
 CBA
 BLS MANDM2
 JSR RANDOM
 ANDA #3
 SEC
 ADCA ,X
 STA ,X
MANDM2 INX
 CPX #SUPFLG
 BNE MANDM1
 TST DAMSHL
 BEQ MANDM3
 CLR SHIELD
MANDM3 RTS
RPTDAM LDX #DMGDST
 JSR PSTRNG
RPTDM8 JMP COMAND
* DAMAGE REPORT.
DAMRPT LDX #DMRPST
 JSR PSTRNG
 LDX #DEVSTR
 STX TEMP2
 LDX #DAMENG
DMRPT2 TST ,X
 BEQ BMPX4
 STX TEMP3
 LDX TEMP2
 JSR PSTRNG
 STX TEMP2
 LDX TEMP3
 LDB #3
OUTS4 JSR OUTS
 DECB
 BNE OUTS4
 LDA ,X
 JSR OUTK0
DMRPT4 INX
 CPX #SUPFLG
 BNE DMRPT2
 BRA RPTDM8
BMPX4 STX TEMP3
 LDX TEMP2
 INX
 INX
 INX
 INX
 STX TEMP2
 LDX TEMP3
 BRA DMRPT4
*
* STRINGS.
TITLE FCC $0A,'*** STAR TREK VER 1.0 ***',4
SHTLNG  FCC $A,'SHORT GAME OR LONG ',4
UPSCAS FCC ' UP',4
BASINF FCC $A,'BASE IN Q',4
DOCKED FCC 'DOCKED',4
DNSCAS FCC ' DN',4
PTEMST FCC 'TUBES EMPTY',4
INTRO1 FCC $A,$A,'DATE: ',4
INTRO2 FCC 'KLINGONS: ',4
INTRO3 FCC 'YEARS: ',4
INTRO4 FCC 'QUADRANT: ',4
INTRO6 FCC 'SECTOR: ',4
COMST FCC $A,'COMMAND: ',4
DWNST FCC 'SHIELDS DOWN!',4
UPSTR FCC 'SHIELDS UP',4
CRSSTR FCC 'COURSE: ',4
WRPSTR FCC 'WARP FACTOR: ',4
BLOKST FCC 'BLOCKED AT S',4
KRMSTR FCC 'RAMMED KLINGON AT S',4
GLBNDS FCC 'GALAXY LIMIT!',4
PHITST FCC 'TORPEDO HIT ',4
PNOENG FCC 'OUT OF ENERGY',4
SDATE FCC ' S.DATE: ',4
CNDTNS FCC ' CNDTN: ',4
YELLOW FCC 'YELLOW',4
RED FCC 'RED',4
GREEN FCC 'GREEN',4
QUADP FCC ' QUDRNT: ',4
SECP FCC ' SECTOR: ',4
ENGSTR FCC ' ENERGY: ',4
KLSTR FCC ' KLINGONS: ',4
SHSTR FCC ' SHLDS: ',4
TRPSTR FCC ' TORPEDO: ',4
CRLFST FCC $D,$A,4
LRSCST FCC $A,'SCAN FOR Q: ',4
MLSHLD FCC 'MUST LOWER SHIELDS!',4
ENAVLB FCC 'ENERGY AVAILABLE: ',4
FIRENG FCC 'ENERGY = ',4
TOMUCH FCC 'ENERGY TO LOW!',4
PHAMIS FCC 'MISFIRE!',4
ALKILL FCC 'ALL LOCAL KLINGONS DESTROYED!',4
KHTADM FCC 'ENEMY DAMAGED',4
ERR FCC ' ? ',4
STARST FCC 'STAR',4
BASEST FCC 'BASE',4
KLGSTR FCC 'KLINGON',4
INTRO0 FCC $A,'PASSWORD.. ',4
STILFT FCC 'KLINGONS LEFT = ',4
KATKDN FCC ' UNITS HIT ON ENTERPRISE!',4
KATKUP FCC 'KLINGONS ATTACK: SHIELDS HOLDING',4
DISINT FCC 'ENTERPRISE DISINTEGRATES!',4
ABORT FCC 'SEQUENCE ABORT: PASSWORD',4
CANTUS FCC 'TOO EARLY!',4
SOMWHR FCC 'MALFUNCTION!',4
SPSTRM FCC $A,$7,'SPACE STORM: SHIELDS DAMAGED!',4
GALDUM FCC $A,$7,'SHIP IS FRIED FOR 3RD ATTEMPT!',4
NMENGS FCC $A,'ENERGY = 0',4
NMTMST FCC $7,'TIME UP!',4
NMKLST FCC $A,$7,'CONGRATULATIONS!',$A,$D,'ALL KLINGONS DESTROYED',4
FAILST FCC 'MISSION A FAILURE!',4
SUCCST FCC 'THE FEDERATION IS SAVED!',4
SUPDES FCC 'SUPERNOVA!',4
SUPSTR FCC $A,$D,'SUPERNOVA IN Q',4
HEVDAM FCC 'BADLY '
DMGDST FCC 'DAMAGED',4
DMRPST FCC $A,'DEV  STAT',4
DEVSTR FCC 'ENG',4,'SRS',4,'LRS',4,'PHS',4,'TRP',4,'SHL',4
PLAYAG FCC $A,'PLAY AGAIN ? ',4
*
*
RAND STB BSAVE
 LDB #8
RPT LDA RNDM+3
 ASLA
 ASLA
 ASLA
 EORA RNDM+3
 ASLA
 ASLA
 ROL RNDM
 ROL RNDM+1
 ROL RNDM+2
 ROL RNDM+3
 DECB
 BNE RPT
 LDB BSAVE
 LDA RNDM
 RTS
BSAVE RMB 1
RNDM RMB 4
*
OUTHL LSRA
 LSRA
 LSRA
 LSRA
OUTHR ANDA #$0F
 ADDA #$30
 CMPA #$39
 LBLS OUTCH
 ADDA #$07
 LBRA OUTCH
*
OUTS LDA #$20
 LBRA OUTCH
*
 END SPAVOY
                                         